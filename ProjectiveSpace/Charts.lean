import Mathlib.Tactic
import Mathlib.LinearAlgebra.ProjectiveSpace.Basic
import Mathlib.Topology.Algebra.Module.Basic
import ProjectiveSpace.Grassmannian 
import Mathlib.Topology.Algebra.Module.FiniteDimension


open Classical
noncomputable section 


section NoTopology 

variable {r : â„•} {ğ•œ E U : Type*} [DivisionRing ğ•œ] [AddCommGroup E] [Module ğ•œ E] [AddCommGroup U] [Module ğ•œ U]

namespace Grassmannian

/- We define what will be charts on Grassmannian ğ•œ E r. The charts are indexed by linear equivalences
Ï† : E â‰ƒâ‚—[ğ•œ] U Ã— (Fin r â†’ ğ•œ), the model of each chart is ((Fin r â†’ ğ•œ) â†’â‚—[ğ•œ] U). The source of the chart is
the set of r-dimensional subspaces W such that Ï†.2 induces an isomorphism W â‰ƒ (Fin r â†’ ğ•œ), or equivalently
such that W intersects Ker Ï†.1 trivially; we call this the Goodset of Ï†.1; its image is all of the codomain.
To have a chart defined at each point, we will need to assume that there exists a linear equivalence between E and 
(Fin r â†’ ğ•œ) Ã— U, but we do that later.-/

/- Definition of the sources and lemmas about it.-/

def Goodset (Ï† : E â†’â‚—[ğ•œ] (Fin r â†’ ğ•œ)) : Set (Grassmannian ğ•œ E r) :=
{W : Grassmannian ğ•œ E r | W.1 âŠ“ (LinearMap.ker Ï†) = âŠ¥}

lemma Goodset_iff_equiv (Ï† : E â†’â‚—[ğ•œ] (Fin r â†’ ğ•œ)) (W : Grassmannian ğ•œ E r) :
W âˆˆ Goodset Ï† â†” Function.Bijective (LinearMap.domRestrict Ï† W.1) := by
  unfold Goodset
  simp only [ge_iff_le, Set.mem_setOf_eq]
  constructor 
  . intro hW 
    have hker : LinearMap.ker (LinearMap.domRestrict Ï† W.1) = âŠ¥ := by
      ext u 
      simp only [LinearMap.mem_ker, LinearMap.domRestrict_apply, Submodule.mem_bot]
      constructor 
      . intro h 
        change u.1 âˆˆ LinearMap.ker Ï† at h 
        have h' := Submodule.mem_inf.mpr âŸ¨u.2, hâŸ©
        rw [hW] at h'
        simp only [Submodule.mem_bot, ZeroMemClass.coe_eq_zero] at h' 
        exact h'
      . exact fun h => by simp only [h, ZeroMemClass.coe_zero, map_zero]
    rw [LinearMap.ker_eq_bot] at hker
    change _ âˆ§ _ 
    rw [and_iff_right hker]
    letI := W.2.1
    refine (LinearMap.injective_iff_surjective_of_finrank_eq_finrank ?_).mp hker 
    rw [W.2.2]
    simp only [FiniteDimensional.finrank_fintype_fun_eq_card, Fintype.card_fin]
  . intro hW 
    ext u
    simp only [ge_iff_le, Submodule.mem_inf, LinearMap.mem_ker, Submodule.mem_bot]
    constructor 
    . intro âŸ¨huW, huÏ†âŸ©
      have huÏ†' : LinearMap.domRestrict Ï† W âŸ¨u, huWâŸ© = 0 := by
        rw [LinearMap.domRestrict_apply]
        exact huÏ†
      rw [â†(LinearMap.map_zero (LinearMap.domRestrict Ï† W ))] at huÏ†'
      have h := hW.1 huÏ†'
      simp only [Submodule.mk_eq_zero] at h   
      exact h
    . exact fun h => by simp only [h, Submodule.zero_mem, map_zero, and_self]

/- Definition of the charts.-/

def Chart (Ï† : E â‰ƒâ‚—[ğ•œ] (Fin r â†’ ğ•œ) Ã— U) : Grassmannian ğ•œ E r â†’ ((Fin r â†’ ğ•œ) â†’â‚—[ğ•œ] U) := by 
  intro W 
  set Ï†â‚ := (LinearMap.fst ğ•œ (Fin r â†’ ğ•œ) U).comp Ï†.toLinearMap
  by_cases hW : W âˆˆ Goodset Ï†â‚
  . rw [Goodset_iff_equiv] at hW  
    exact ((LinearMap.snd ğ•œ (Fin r â†’ ğ•œ) U).comp Ï†.toLinearMap).comp ((Submodule.subtype W.1).comp 
      (LinearEquiv.ofBijective (Ï†â‚.domRestrict W.1) hW).symm.toLinearMap) 
  . exact 0 

/- Definition of the inverse chart.-/


def InverseChart (Ï† : E â‰ƒâ‚—[ğ•œ] (Fin r â†’ ğ•œ) Ã— U) : ((Fin r â†’ ğ•œ) â†’â‚—[ğ•œ] U) â†’ Grassmannian ğ•œ E r := by 
  intro f 
  refine âŸ¨Submodule.map Ï†.symm (LinearMap.graph f), ?_âŸ©
  unfold Grassmannian
  simp only [Set.mem_setOf_eq]
  constructor
  . letI := LinearEquiv.finiteDimensional (LinearMap.graph_equiv_fst f).symm 
    apply Module.Finite.map 
  . erw [LinearEquiv.finrank_map_eq Ï†.symm]
    rw [LinearEquiv.finrank_eq (LinearMap.graph_equiv_fst f)]
    simp only [FiniteDimensional.finrank_fintype_fun_eq_card, Fintype.card_fin]

lemma InverseChart_codomainGoodset (Ï† : E â‰ƒâ‚—[ğ•œ] (Fin r â†’ ğ•œ) Ã— U) (f : (Fin r â†’ ğ•œ) â†’â‚—[ğ•œ] U) :
InverseChart Ï† f âˆˆ Goodset ((LinearMap.fst ğ•œ (Fin r â†’ ğ•œ) U).comp Ï†.toLinearMap) := by
  rw [Goodset_iff_equiv]
  unfold InverseChart
  simp only
  erw [LinearMap.coe_comp]
  simp only [LinearMap.coe_comp, LinearEquiv.coe_coe, Submodule.coeSubtype]
  rw [Set.bijective_iff_bijOn_univ]
  apply Set.BijOn.comp (t := Submodule.map (LinearEquiv.symm Ï†) (LinearMap.graph f))
  . apply Set.BijOn.comp (t := LinearMap.graph f)
    . apply Set.BijOn.mk
      . apply Set.mapsTo_univ
      . rw [Set.injOn_iff_injective]
        exact LinearMap.graph_fst_injective f         
      . rw [Set.surjOn_iff_surjective]
        exact LinearMap.graph_fst_surjective f                 
    . simp only [Submodule.map_coe]
      apply Equiv.bijOn' Ï†.toEquiv 
      . simp only [LinearEquiv.coe_toEquiv, Set.maps_image_to]
        intro u
        simp only [SetLike.mem_coe, LinearMap.mem_graph_iff, Function.comp_apply, LinearEquiv.apply_symm_apply,
          imp_self]
      . simp only [LinearEquiv.coe_toEquiv_symm]
        intro u
        simp only [SetLike.mem_coe, LinearMap.mem_graph_iff, Set.mem_image, Prod.exists, exists_eq_left]
        intro hu
        existsi u.1 
        rw [â†hu]
        simp only [Prod.mk.eta]
        rfl
  . constructor 
    . simp only [Submodule.map_coe, Set.maps_univ_to, Set.mem_image, SetLike.mem_coe, LinearMap.mem_graph_iff,
      Prod.exists, exists_eq_left, Subtype.forall, Submodule.mem_map, forall_exists_index, forall_apply_eq_imp_iff',
      EmbeddingLike.apply_eq_iff_eq, Prod.mk.injEq, forall_const]
    . rw [and_iff_right Set.injOn_subtype_val]
      have heq : Submodule.map (LinearEquiv.symm Ï†) (LinearMap.graph f) = (fun (x : Submodule.map 
        (LinearEquiv.symm Ï†) (LinearMap.graph f)) => x.1) '' Set.univ := by
        simp only [Submodule.map_coe, Set.image_univ, Subtype.range_coe_subtype, Submodule.mem_map,
          LinearMap.mem_graph_iff, Prod.exists, exists_eq_left]
        ext u
        simp only [Set.mem_image, SetLike.mem_coe, LinearMap.mem_graph_iff, Prod.exists, exists_eq_left,
          Set.mem_setOf_eq]
      rw [heq]
      apply Set.surjOn_image 

/- We prove that the charts are inverses.-/

lemma InverseChartChart_aux1 (Ï† : E â‰ƒâ‚—[ğ•œ] (Fin r â†’ ğ•œ) Ã— U) {x : Grassmannian ğ•œ E r}
(hx : x âˆˆ Goodset ((LinearMap.fst ğ•œ (Fin r â†’ ğ•œ) U).comp Ï†.toLinearMap)) {u : E} (hu : u âˆˆ x.1) :
(Chart Ï† x) (Ï† u).1 = (Ï† u).2 := by
  unfold Chart
  simp only [hx, dite_true, LinearMap.coe_comp, LinearEquiv.coe_coe, Submodule.coeSubtype, Function.comp_apply,
    LinearMap.snd_apply]
  have heq : (Ï† u).1 = ((LinearMap.fst ğ•œ _ _).comp Ï†.toLinearMap).domRestrict x.1 âŸ¨u, huâŸ© := by
    simp only [LinearMap.domRestrict_apply, LinearMap.coe_comp, LinearEquiv.coe_coe, Function.comp_apply,
      LinearMap.fst_apply]
  rw [heq]
  rw [â†LinearEquiv.invFun_eq_symm]
  erw [LinearEquiv.left_inv]
 

lemma InverseChartChart_aux2 (Ï† : E â‰ƒâ‚—[ğ•œ] (Fin r â†’ ğ•œ) Ã— U) {x : Grassmannian ğ•œ E r}
(hx : x âˆˆ Goodset ((LinearMap.fst ğ•œ (Fin r â†’ ğ•œ) U).comp Ï†.toLinearMap)) :
Submodule.map Ï† x.1 = LinearMap.graph (Chart Ï† x) := by
  letI := LinearEquiv.finiteDimensional (LinearMap.graph_equiv_fst (Chart Ï† x)).symm
  apply FiniteDimensional.eq_of_le_of_finrank_eq
  . intro u 
    simp only [Submodule.mem_map, LinearMap.mem_graph_iff, forall_exists_index, and_imp] 
    intro v hvx hvu 
    rw [â†hvu]
    apply Eq.symm
    exact InverseChartChart_aux1 Ï† hx hvx
  . erw [LinearEquiv.finrank_map_eq Ï†]
    rw [LinearEquiv.finrank_eq (LinearMap.graph_equiv_fst (Chart Ï† x)), x.2.2]
    simp only [FiniteDimensional.finrank_fintype_fun_eq_card, Fintype.card_fin]


lemma InverseChartChart (Ï† : E â‰ƒâ‚—[ğ•œ] (Fin r â†’ ğ•œ) Ã— U) {x : Grassmannian ğ•œ E r} 
(hx : x âˆˆ Goodset ((LinearMap.fst ğ•œ (Fin r â†’ ğ•œ) U).comp Ï†.toLinearMap)) :
InverseChart Ï† (Chart Ï† x) = x := by
  unfold InverseChart 
  simp_rw [â†(InverseChartChart_aux2 Ï† hx)]
  ext u
  simp only [Submodule.mem_map, exists_exists_and_eq_and, LinearEquiv.symm_apply_apply, exists_eq_right]
   

lemma ChartInverseChart_aux (Ï† : E â‰ƒâ‚—[ğ•œ] (Fin r â†’ ğ•œ) Ã— U) (f : (Fin r â†’ ğ•œ) â†’â‚—[ğ•œ] U) {u : E}
(hu : u âˆˆ Ï†.symm '' (LinearMap.graph f)) :
(Ï† u).2 = f (Ï† u).1 := by
  rw [LinearEquiv.image_symm_eq_preimage] at hu
  simp only [Set.mem_preimage, SetLike.mem_coe, LinearMap.mem_graph_iff] at hu 
  exact hu

lemma ChartInverseChart (Ï† : E â‰ƒâ‚—[ğ•œ] (Fin r â†’ ğ•œ) Ã— U) (f : (Fin r â†’ ğ•œ) â†’â‚—[ğ•œ] U) :
Chart Ï† (InverseChart Ï† f) = f := by
  unfold Chart
  simp only [InverseChart_codomainGoodset, dite_true]
  apply LinearMap.ext 
  intro v 
  simp only [LinearMap.coe_comp, LinearEquiv.coe_coe, Submodule.coeSubtype, Function.comp_apply, LinearMap.snd_apply]
  rw [ChartInverseChart_aux Ï† f]
  . apply congrArg
    change LinearMap.fst ğ•œ _ _ _ = v 
    erw [â†(LinearMap.comp_apply (f := LinearMap.fst ğ•œ _ _) (g := Ï†.toLinearMap))]
    have hf := InverseChart_codomainGoodset Ï† f
    rw [Goodset_iff_equiv] at hf  
    erw [â†(LinearMap.comp_apply (f := LinearMap.comp (LinearMap.fst ğ•œ _ _) Ï†.toLinearMap) (x := v)
      (g := LinearMap.comp (Submodule.subtype _) (LinearEquiv.symm (LinearEquiv.ofBijective _ hf)).toLinearMap))]
    conv => rhs 
            rw [â†(LinearMap.id_apply (R := ğ•œ) (M := Fin r â†’ ğ•œ) v)]
    apply congrFun
    rw [â†LinearMap.comp_assoc]
    change 
      â†‘(LinearMap.comp (LinearMap.domRestrict (LinearMap.comp (LinearMap.fst ğ•œ _ _) Ï†.toLinearMap) (InverseChart Ï† f).1) 
      (LinearEquiv.symm (LinearEquiv.ofBijective _ hf)).toLinearMap) = fun v => LinearMap.id v 
    simp only [LinearMap.coe_comp, LinearEquiv.coe_coe, LinearMap.id_coe, id_eq] 
    rw [â†LinearEquiv.invFun_eq_symm]
    have heq : (LinearMap.domRestrict (LinearMap.comp (LinearMap.fst ğ•œ (Fin r â†’ ğ•œ) U) Ï†.toLinearMap) (InverseChart Ï† f).1) =
      (LinearEquiv.ofBijective _ hf).toLinearMap := by
      ext u
      simp only [LinearMap.domRestrict_apply, LinearMap.coe_comp, LinearEquiv.coe_coe, Function.comp_apply,
        LinearMap.fst_apply, LinearEquiv.ofBijective_apply]
    nth_rewrite 1 [heq]
    erw [â†LinearEquiv.toFun_eq_coe]
    ext v 
    simp only [AddHom.toFun_eq_coe, LinearMap.coe_toAddHom, LinearEquiv.coe_coe, LinearEquiv.invFun_eq_symm,
      Function.comp_apply, LinearEquiv.apply_symm_apply]   
  . change _ âˆˆ Submodule.map Ï†.symm (LinearMap.graph f)
    unfold InverseChart
    simp only [SetLike.coe_mem]

/- Definition of the chart as LocalEquiv.-/

def Chart_LocalEquiv (Ï† : E â‰ƒâ‚—[ğ•œ] (Fin r â†’ ğ•œ) Ã— U) : LocalEquiv (Grassmannian ğ•œ E r) ((Fin r â†’ ğ•œ) â†’â‚—[ğ•œ] U) :=
{
  toFun := Chart Ï†
  invFun := InverseChart Ï† 
  source := Goodset ((LinearMap.fst ğ•œ (Fin r â†’ ğ•œ) U).comp Ï†.toLinearMap)
  target := âŠ¤
  map_source' := by tauto 
  map_target' := fun f _ => InverseChart_codomainGoodset Ï† f
  left_inv' := fun _ hW  => InverseChartChart Ï† hW  
  right_inv' := fun f _ => ChartInverseChart Ï† f   
}

end Grassmannian
end NoTopology

section Topology

namespace Grassmannian

variable {ğ•œ E U : Type*} [NormedDivisionRing ğ•œ] [NormedAddCommGroup E] [Module ğ•œ E] [BoundedSMul ğ•œ E]
[NormedAddCommGroup U] [Module ğ•œ U] [BoundedSMul ğ•œ E] [CompleteSpace ğ•œ]

def ContChart (Ï† : E â‰ƒâ‚—[ğ•œ] (Fin r â†’ ğ•œ) Ã— U) : Grassmannian ğ•œ E r â†’ ((Fin r â†’ ğ•œ) â†’L[ğ•œ] U) := by
  intro W
  exact LinearMap.toContinuousLinearMap (ğ•œ := ğ•œ) (E := Fin r â†’ ğ•œ) (Chart Ï† W) 


end Grassmannian
end Topology



#exit 


class MySpecialEquiv (ğ•œ E U : Type*) [DivisionRing ğ•œ] [AddCommGroup E] [Module ğ•œ E] [AddCommGroup U] [Module ğ•œ U] (r : â„•) :=
  (myEquiv : E â‰ƒâ‚—[ğ•œ] (Fin r â†’ ğ•œ) Ã— U)

variable {Îµ : MySpecialEquiv ğ•œ E U r}

end 

